plugins {
    id 'groovy'
    id 'java-library'
}

version = projectVersion
group = 'org.grails'

ext {
    apiDocs = true
    mavenSnapshotPublishUrl = 'https://repo.grails.org/grails/libs-snapshots-local'
}

dependencies {

    implementation platform("org.grails:grails-bom:$grailsVersion")

    api 'org.mongodb:bson', {
        // api: Document, ObjectId
        // impl: Bson
    }
    api 'org.mongodb:mongodb-driver-core', {
        // api: DBObject, DeleteResult, ReadPreference, @Nullable(runtime), UpdateResult, WriteConcern
        // impl: BasicDBObject
    }

    implementation project(':grails-datastore-gorm-mongodb'), {
        // impl: AbstractMongoObectEntityPersister, AbstractMongoSession, MongoConstants, MongoEntityPersister,
        //       MongoQuery
        if (excludeUnusedTransDeps) {
            // API dependencies in grails-datastore-gorm-mongodb
            //exclude group: 'org.grails', module: 'grails-datastore-core' // NativeEntryEntityPersister
            exclude group: 'org.grails', module: 'grails-datastore-gorm'
            exclude group: 'org.grails', module: 'grails-datastore-gorm-bson'
            exclude group: 'org.grails', module: 'grails-datastore-gorm-support'
            //exclude group: 'org.mongodb', module: 'bson' // Document
            exclude group: 'org.mongodb', module: 'mongodb-driver-core'
            //exclude group: 'org.mongodb', module: 'mongodb-driver-sync' // FindIterable
            exclude group: 'org.springframework', module: 'spring-context'
        }
    }
    implementation 'org.grails:grails-datastore-gorm', {
        // impl: GormEnhancer
        if (excludeUnusedTransDeps) {
            // API dependencies in grails-datastore-gorm
            exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
            //exclude group: 'jakarta.persistence', module: 'jakarta.persistence-api' // Entity
            exclude group: 'org.grails', module: 'grails-core'
            exclude group: 'org.grails', module: 'grails-datastore-core'
            exclude group: 'org.grails', module: 'grails-datastore-gorm-validation'
            //exclude group: 'org.springframework', module: 'spring-context' // ApplicationEventPublisher
            exclude group: 'org.springframework', module: 'spring-jdbc'
            //exclude group: 'org.springframework', module: 'spring-tx' // PlatformTransactionManager
        }
    }

    compileOnly 'org.apache.groovy:groovy'
    compileOnly 'org.slf4j:slf4j-api', {
        // "optional" compile dependency in mongodb-driver-core
    }
}

apply from: rootProject.layout.projectDirectory.file('gradle/java-config.gradle')
apply from: rootProject.layout.projectDirectory.file('gradle/publish-config.gradle')
